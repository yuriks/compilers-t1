@@skip := "(?:\s*(?://[^\n]*\n)?)*" ;

@token-id := "(@@?[a-z]+(?:-[a-z]+)*)" ;
@rule-id := "(#?[a-z]+(?:-[a-z]+)*)" ;
@token-literal := "'((?:\\.|[^'\n])+)'" ;
@token-match := "\"((?:\\.|[^\"\n])+)\"" ;

token-decl := @token-id => ':=' => @token-match ;
rule-decl := @rule-id => ':=' => rule-alternation-expr ;

rule-atom := @token-id | @rule-id | @token-literal | '(' => rule-alternation-expr => ')' ;
rule-postfix-expr := rule-atom => ( '?' | '*' )? ;
rule-follow-expr := rule-postfix-expr => ( '=>' => rule-postfix-expr )* ;
rule-alternation-expr := rule-follow-expr => ( '|' => rule-follow-expr )* ;

#grammar := ( ( token-decl | rule-decl ) => ';' )* ;
